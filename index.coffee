#!/usr/bin/env coffee

assert = require 'assert'
co = require 'co'
fs = require 'fs'
instapromise = require 'instapromise'
{
  isString
} = require 'lodash-node'
minimist = require 'minimist'
path = require 'path'
util = require 'util'

findNativePackagesAsync = co.wrap (dir='.') ->
  """Finds all the React Native modules from a given directory"""

  foundModules = []

  pkg = require path.join dir, 'package.json'
  if pkg.nativePackage?
    foundModules.push {
      path: path.relative '.', dir
      name: pkg.name
      version: pkg.version
      nativePackage: pkg.nativePackage
      pkg
    }

  node_modules = path.join dir, 'node_modules'

  if fs.existsSync node_modules # N.B. Watch out for `fs.exists` -- it doesn't conform to the (err, result) protocol
    awaitables = []
    for mod in yield fs.promise.readdir node_modules
      unless mod[0] is '.' # Ignore hidden files/dirs like '.bin'
        awaitables.push findNativePackagesAsync path.join node_modules, mod

    foundModules.concat (yield awaitables)...
  else
    foundModules

rubyEscapeString = (s) ->
  """Escapes a string for Ruby (for use in Podfiles)"""

  # This is a little bit of a hack but it should be correct
  assert isString s
  util.inspect s

podfileFragmentAsync = co.wrap (dir='.') ->
  """Returns the text of the Podfile we use for REM"""

  nativePackages = yield findNativePackagesAsync dir
  podspecs = []
  for np in nativePackages
    if np.nativePackage.podspec?
      podspecs.push [np.name, path.join np.path, np.nativePackage.podspec]
    else
      defaultPodspec = path.join np.path, "#{ np.name }.podspec"
      if fs.existsSync defaultPodspec
        podspecs.push [np.name, defaultPodspec]

  """
  # Generated by REM2
  #{("pod #{ rubyEscapeString name }, :path => #{ rubyEscapeString podspecPath }\n" for [name, podspecPath] in podspecs).join ''}
  """

module.exports = {
  findNativePackagesAsync
  podfileFragmentAsync
}

if require.main is module
  args = minimist process.argv.slice 2
  podfileFragmentAsync(args._[0]).then console.log, console.error
